apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.properties.get('compileSdkVersion', 26)
    buildToolsVersion rootProject.properties.get('buildToolsVersion', '27.0.3')

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion rootProject.properties.get('targetSdkVersion', 26)
        versionCode 35
        versionName '1.10.0'
        ndk {
            abiFilters 'armeabi-v7a', 'x86'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //noinspection GradleDynamicVersion
    compile ('com.facebook.react:react-native:+') {
        exclude group: 'com.android.support'
    }
    compile ('com.microsoft.appcenter:appcenter-push:1.10.0') {
        exclude group: 'com.google.firebase'
    }
    compile 'com.google.firebase:firebase-core:16.0.4'
    compile 'com.google.firebase:firebase-messaging:17.3.4'

    /*
     * For projects that needs to execute :verifyReleaseResources yet have :lint also succeed,
     * we need to depend on the same support lib version of the app.
     * If we just need :lint to work, excluding the support libs from push/react native is enough,
     * so if ext.supportLibVersion is undefined that should still work for older apps.
     */
    def supportLibVersion = rootProject.properties.get('supportLibVersion');
    if (supportLibVersion != null) {
        compile "com.android.support:appcompat-v7:${supportLibVersion}"
        compile "com.android.support:support-v4:${supportLibVersion}"
    }

    //compile project(':AppCenterReactNativeShared')   // For testing with TestApp
    compile 'com.microsoft.appcenter.reactnative:appcenter-react-native:1.10.0'
}
